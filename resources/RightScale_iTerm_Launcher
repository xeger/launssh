#!/usr/bin/osascript

on run argv
	try
		set AppleScript's text item delimiters to " "
		set cmd to (argv as text)
		
    set sshWindow to false

		tell application "iTerm"
			activate

      # Find a window that already has an SSH session in the foreground tab by looking at session
      # names and finding one with "ssh" in its title.
      repeat with aTerminal in terminals
        repeat with aSession in sessions of aTerminal
          if (name of aSession as string) contains "ssh" then
            set sshWindow to aTerminal
            exit repeat
          end if
        end repeat
        if sshWindow ≠ false then exit repeat
      end repeat

      # If no SSH window was found, use the front window.
      if sshWindow = false then
        set sshWindow to (get current terminal)
      end if

			tell sshWindow
				set sshTab to (make new session)
				tell sshTab
				  exec command "ssh " & cmd
				end tell
			end tell
		end tell
	on error errStr number errorNumber
    display alert "SSH launch failed" ¬
      message "AppleScript error " & errorNumber & "\n" & errStr ¬
      as critical
	end try
end run
